Window: struct = {
    #(public)
    init: fn(w: i32, h: i32, title: &str) -> Window = {
        init_window_i: fn(w: i32, h: i32, title: ptr) = c_import!("InitWindow");
        init_window_i(w, h, title.c_str());
        return Window{};
    }
    #(public)
    should_close: fn(&this) -> bool  = {
       should_close_i: fn -> bool = c_import!("WindowShouldClose");
       return should_close_i();
    }
    #(destructor)
    destroy: fn(&mut this) = {
        dst: fn = c_import!("CloseWindow");
        dst();       
    }
}
AudioDevice: struct = {
    #(public)
    init: fn -> AudioDevice = {
        init_ad: fn = c_import!("InitAudioDevice");
        init_ad();
        return This{};
    }
    #(destructor)
    destroy: fn(&mut this) = {
        dst: fn = c_import!("CloseAudioDevice");
        dst();
    }
}
AudioStream: struct = {
    buffer: ptr,
    processor: ptr,
    sample_rate: u32,
    sample_size: u32,
    channels: u32
}
Sound: struct = {
    stream: AudioStream,
    frame_count: u32,
    #(public)
    load: fn(src: &str) -> Sound = {
        load_sound_i: fn(file: ptr) -> This = c_import!("LoadSound");
        return load_sound_i(src.c_str());
    }
    #(public)
    play: fn(&this) = {
        play_sound: fn(sound: Sound) = c_import!("PlaySound");
        play_sound(*this);
    }
    #(destructor)
    unload: fn(&mut this) = {
        unload_sound: fn(sound: Sound) = c_import!("UnloadSound");
        unload_sound(*this);
    }
}

#(public)
set_target_fps: fn(fps: i32) = c_import!("SetTargetFPS");

#(public)
Key: enum = {
    Space = 32, Enter = 257, C = 67,
    #(public)
    is_key_pressed: fn(this) -> bool = c_import!("IsKeyPressed");
}

#(public)
begin_drawing: fn = c_import!("BeginDrawing");

#(public)
end_drawing: fn = c_import!("EndDrawing");

#(public)
Color: struct = {
    r: u8, g: u8, b: u8, a: u8,
    #(public)
    RAY_WHITE: const Color = Color{.r = 245, .g = 245, .b = 245, .a = 245};
    #(public)
    LIGHT_GRAY: const Color = Color{.r = 200, .g = 200, .b = 200, .a = 255};
}

#(public)
draw_text: fn(text: &str, posx: i32, posy: i32, fontsz: i32, color: Color) = {
    dtxt_i: fn(text: ptr, px: i32, py: i32, font: i32, color: Color)
        = c_import!("DrawText");
    dtxt_i(text.c_str(), posx, posy, fontsz, color);
}

#(public)
draw_rectangle: fn(posx: i32, posy: i32, width: i32, height: i32, color: Color) =
    c_import!("DrawRectangle");

#(public)
clear_background: fn(color: Color) = c_import!("ClearBackground");

#(public)
get_mouse_x: fn -> i32 = c_import!("GetMouseX");
#(public)
get_mouse_y: fn -> i32 = c_import!("GetMouseY");
#(public)
is_mouse_pressed: fn(btn: i32) -> bool = c_import!("IsMouseButtonPressed");
